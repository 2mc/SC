title:: Subtropic Drumming
categories:: UGens>Filters, UGens>Noise
summary:: Example using RLPF and LFNoise0 to create a rhythmic pattern
related:: Classes/LFNoise0, Classes/RLPF

note::
This little code was used to record a 60" stereo sample for the TV spot of the Ionian University in March 2012. 
::

code::
// Get ready 1: Boot the server
Server.default.boot;
::

code::
// Get ready 2: Send the synthdef
(
SynthDef("subtropicdrumming", { |freq = 300, amp = 1.0, dur = 10, rate1 = 5, rate2 = 2.51 |
	var out;
	out = RLPF.ar(
		// in 
		LFTri.ar(
			// src freq
			Demand.kr(Impulse.kr([rate1, rate2]), 0, 
				Dser((200, 300..1100).scramble, inf)), 0, 0.05
			),
		// rez freq
		LFNoise0.kr([0.2,0.21], freq, freq * 4 / 3),
		// rq, mul 
		0.01, amp
	);
	out = Limiter.ar(out, 0.5);	
	Out.ar(0, out * EnvGen.kr(Env([0, 1, 0.1, 0.3, 0], [dur, dur, dur, dur] / 4), doneAction: 2));
}, [\ir,\ir,\ir]).add;
)
::

code::
// Go: start both synths together to keep the beats synchronized
(
a = Synth("subtropicdrumming", [\dur, 30, \freq, 800, \amp, 0.2, \rate1, 5, \rate2, 2.5]);
b = Synth("subtropicdrumming", [\dur, 60, \freq, 300, \amp, 0.3, \rate1, 10, \rate2, 5.0]);
)
::

